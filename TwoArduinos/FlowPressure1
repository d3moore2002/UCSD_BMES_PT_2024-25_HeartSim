// -----------------------------------------------
// Arduino 1: Pressure + Flow Sensor Board
// -----------------------------------------------
#include <Wire.h>
#include <SparkFun_I2C_Mux_Arduino_Library.h>
#include <SparkFun_MicroPressure.h>

const float PSI_TO_MMHG = 51.7149;
QWIICMUX myMux;
const uint8_t muxAddress = 0x70;
const int sensorChannels[3] = {0, 2, 3};
SparkFun_MicroPressure pressureSensor;

const int flowPin = 3;
volatile int flowPulseCount = 0;
float calibrationFactor = 5.5;
unsigned long lastRead = 0;
const unsigned long readInterval = 940;

void flowISR() {
  flowPulseCount++;
}

float readPressureSensor(int channel) {
  myMux.setPort(channel);
  if (!pressureSensor.begin()) return -1.0;
  float pressure_psi = pressureSensor.readPressure();
  return pressure_psi * PSI_TO_MMHG;
}

void setup() {
  Serial.begin(9600);
  Wire.begin();
  if (!myMux.begin(muxAddress)) {
    while (1);
  }
  pinMode(flowPin, INPUT_PULLUP);
  attachInterrupt(digitalPinToInterrupt(flowPin), flowISR, RISING);
  lastRead = millis();
}

void loop() {
  unsigned long now = millis();
  if (now - lastRead >= readInterval) {
    for (int i = 0; i < 3; i++) {
      float pressure = readPressureSensor(sensorChannels[i]);
      Serial.print("Pressure Sensor ");
      Serial.print(i);
      Serial.print(": ");
      Serial.print(pressure);
      Serial.println(" mmHg");
    }
    noInterrupts();
    int count = flowPulseCount;
    flowPulseCount = 0;
    interrupts();
    float flowRate_L_per_min = count / calibrationFactor;
    float flowRate_mL_per_sec = (flowRate_L_per_min * 1000) / 60.0;
    Serial.print("Flow Rate: ");
    Serial.print(flowRate_mL_per_sec);
    Serial.println(" mL/sec");
    lastRead = now;
  }
}

